buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['Adgeist_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Adgeist_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.adgeist"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      if(isNewArchitectureEnabled()){
        java.srcDirs += [
          "src/newarch",

          // This is needed to build Kotlin project with NewArch enabled
          "${project.buildDir}/generated/source/codegen/java",
          "${project.buildDir}/generated/source/codegen/jni"

          // "generated/java",
          // "generated/jni"
        ]
      }else{
        java.srcDirs += ["src/oldarch"]
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
  /*
   * Alternative: Use local AAR file instead of remote package
   * Uncomment the lines below and comment out the remote dependency above to use a local AAR file.
   * This is useful for testing unreleased versions or when working with custom builds.
   * Place your AAR file in the 'libs' folder.
   */
  // flatDir {
  //   dirs 'libs'
  // }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "ai.adgeist:adgeistkit:1.1.20-beta"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  /*
   * Alternative: Use local AAR file instead of remote package
   * Uncomment the lines below and comment out the remote dependency above to use a local AAR file.
   * This is useful for testing unreleased versions or when working with custom builds.
   * Place your AAR file in the 'libs' folder.
   */
  // implementation(name: 'adgeistkit-beta-release', ext: 'aar')
  // implementation "androidx.work:work-runtime-ktx:2.11.1"
  // implementation 'com.google.code.gson:gson:2.10.1'
  // implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  // implementation("com.google.android.gms:play-services-ads-identifier:18.0.1")
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "Adgeist"
    codegenJavaPackageName = "com.adgeist"
  }
}
